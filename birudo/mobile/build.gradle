import org.ajoberstar.grgit.Grgit

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.ajoberstar.release-opinion'
apply plugin: 'play'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))

def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def getGooglePlaystoreServiceAccountEmail() {
    return project.hasProperty('gPlaystoreServiceAccountEmailAddress') ? gPlaystoreServiceAccountEmailAddress : ''
}

def getGooglePlaystoreServicepk12File() {
    // Set to home directory if property not set because play plugin doesn't allow empty or null
    // value for the pk file
    return project.hasProperty('gPlaystorePKFile') ? gPlaystorePKFile : '~/'
}

def buildVersionCode() {
    def versionName = android.defaultConfig.versionName
    def (major, minor, patch) = versionName.find(/^(\d+\.\d+\.\d+)/).toString().tokenize('.')
    def code = major.toInteger() * 1000000 + minor.toInteger() * 10000 + patch.toInteger() + 100
    return code
}

def readWhatsNewFile(filename) {
    File file = new File('birudo/mobile/src/main/play/en-US/' + filename);
    return file.exists() ? file.text + '\n\n' : new File('app/src/main/play/en-US/whatsnew').text;
}

def trackProperty() {
    return project.hasProperty('uploadTrack') ? uploadTrack : 'alpha'
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.addhen.birudo"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        releaseSign
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseSign
        }
    }
}

play {
    serviceAccountEmail = getGooglePlaystoreServiceAccountEmail()
    pk12File = file(getGooglePlaystoreServicepk12File())
    uploadImages = true
    track = trackProperty()
}

release {
    grgit = Grgit.open(dir: '.')

    def tagMessage = readWhatsNewFile("whatsnew-${trackProperty()}");

    android.defaultConfig.versionName = project.version.toString()

    android.defaultConfig.versionCode = buildVersionCode().toInteger()

    tagStrategy {
        generateMessage = { version -> "Release of $version.version $tagMessage" }
    }
}

task releaseApp(dependsOn: ['clean', 'assemble', 'release', 'publishRelease']) {
    description 'Release a new version of application, increase version and create tag'
    doLast {
        println "${android.defaultConfig.versionName} released"
    }
}

if (project.hasProperty('releaseKeyStore') &&
        project.hasProperty('releaseKeyPassword') &&
        project.hasProperty('releaseKeyStorePassword') &&
        project.hasProperty('releaseKeyAlias')) {
    android.signingConfigs.releaseSign.keyAlias = releaseKeyAlias
    android.signingConfigs.releaseSign.storeFile = file(releaseKeyStore)
    android.signingConfigs.releaseSign.storePassword = releaseKeyStorePassword
    android.signingConfigs.releaseSign.keyPassword = releaseKeyPassword
} else {
    android.signingConfigs.releaseSign.keyAlias ""
    android.signingConfigs.releaseSign.storeFile null
    android.signingConfigs.releaseSign.storePassword ""
    android.signingConfigs.releaseSign.keyPassword ""
}

dependencies {
    def core = project(':core')
    def data = project(':data')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':birudo:wear')
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup:otto:1.3.6'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'net.danlew:android.joda:2.7.1'
    compile 'com.nispok:snackbar:2.9.0'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile core
    compile data
}
